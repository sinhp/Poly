/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour
-/

import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Closed.Monoidal
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Adjunction.Mates
import Mathlib.CategoryTheory.Limits.Constructions.Over.Basic
import Mathlib.CategoryTheory.Adjunction.Over
import Mathlib.CategoryTheory.IsConnected
import Mathlib.Tactic.ApplyFun
import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts

import Poly.Basic -- some isos in here


/-!
# Expontentiable morphisms in a category

We say that a morphism `f : X âŸ¶ Y` in a category `C` has pushforward if there is
a right adjoint to the base-change functor along `f`.
The type `Pushforward f` is a structure containing `functor : Over X â¥¤ Over Y` and
a witness `adj : baseChange f âŠ£ functor`.

We prove that if a morphism `f : X âŸ¶ Y` has pushforwards then `f` is exponentiable in the
slice category `Over Y`.
In particular, for a morphism `g : X âŸ¶ I` the exponential `f^* g` is the functor composition `(baseChange g) â‹™ (Over.map g)`.

## Notation

We provide the following notations:

* `Î _ f` is the functor `pushforward f : Over J â¥¤ Over I`. As such, for an object
`X : Over J`, we have `Î _f X : Over I`

-/

noncomputable section

open CategoryTheory Category MonoidalCategory Limits Functor Adjunction IsConnected Over


variable {C : Type*} [Category C] [HasPullbacks C]

class CartesianExponentiable {X Y : C} (f : X âŸ¶ Y) where
  /-- A functor `C/X â¥¤ C/Y` right adjoint to `f*`. -/
  functor : Over X â¥¤ Over Y
  adj : baseChange f âŠ£ functor := by infer_instance

@[inherit_doc]
prefix:90 "Î _ " => CartesianExponentiable.functor

namespace CartesianExponentiable

variable {C : Type*} [Category C] [HasPullbacks C]

attribute [local instance] monoidalOfHasFiniteProducts

/-- The identity morphisms `ğŸ™` are exponentiable. -/
instance id {I : C} : CartesianExponentiable (ğŸ™ I) where
  functor := ğŸ­ (Over I)
  adj := by
    fapply ofNatIsoLeft (F:= ğŸ­ _) ?adj (baseChange.id I).symm
    exact Adjunction.id

instance comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    [fexp : CartesianExponentiable f] [gexp : CartesianExponentiable g] :
    CartesianExponentiable (f â‰« g) where
  functor := (Î _ f) â‹™ (Î _ g)
  adj := ofNatIsoLeft (gexp.adj.comp fexp.adj) (baseChange.comp f g).symm

/-- The conjugate isomorphism between pushforward functors. -/
def pushforwardCompIso [HasPullbacks C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [fexp : CartesianExponentiable f] [gexp : CartesianExponentiable g] :
    fexp.functor â‹™ gexp.functor â‰… (comp f g).functor :=
  conjugateIsoEquiv (gexp.adj.comp fexp.adj) ((comp f g).adj) (baseChange.comp f g)

/-- An arrow with a pushforward is exponentiable in the slice category. -/
instance exponentiableOverMk [HasFiniteWidePullbacks C] {I : C} (f : X âŸ¶ I) [CartesianExponentiable f] : Exponentiable (Over.mk f) where
  rightAdj :=  (Î”_ f) â‹™ (Î _ f)
  adj := by
    fapply ofNatIsoLeft
    fapply (Î”_ f) â‹™ (Î£_ f)
    Â· apply Adjunction.comp
      Â· exact CartesianExponentiable.adj
      Â· apply Over.mapAdjunction
    Â· exact baseChange.natIsoTensorLeftOverMk f

end CartesianExponentiable
