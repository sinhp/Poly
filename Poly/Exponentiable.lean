/-
Copyright (c) 2024 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour, Wojciech Nawrocki
-/

import Mathlib.CategoryTheory.Closed.Cartesian

import Poly.Basic

/-!
# Exponentiable morphisms in a category

We define *cartesian exponentiable* morphisms,
and prove that if a morphism `f : X âŸ¶ Y` is cartesian exponentiable
then `f` is exponentiable in the slice category `Over Y`.
In particular, for a morphism `g : I âŸ¶ Y`,
the exponential `f^* g` is the functor composition `(baseChange g) â‹™ (Over.map g)`.

## Notation

We provide the following notations:

* `Î _ f` is the functor `Over X â¥¤ Over Y`.
As such, for an object `X : Over X`, we have `Î _ f X : Over Y`.
-/

noncomputable section

open CategoryTheory Limits Adjunction

variable {C : Type*} [Category C] [HasPullbacks C]

/-- A morphism `f : X âŸ¶ Y` in a category `C` is *cartesian exponentiable*
if there is a right adjoint to the base-change functor along `f`. -/
class CartesianExponentiable {X Y : C} (f : X âŸ¶ Y) where
  /-- A functor `C/X â¥¤ C/Y` right adjoint to `f*`. -/
  functor : Over X â¥¤ Over Y
  adj : Over.pullback f âŠ£ functor := by infer_instance

@[inherit_doc]
prefix:90 "Î _ " => CartesianExponentiable.functor

namespace CartesianExponentiable

/-- The identity morphisms `ğŸ™` are cartesian exponentiable. -/
instance id {I : C} : CartesianExponentiable (ğŸ™ I) where
  functor := ğŸ­ (Over I)
  adj := ofNatIsoLeft (F := ğŸ­ _) Adjunction.id (baseChange.id I).symm

instance comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    [fexp : CartesianExponentiable f] [gexp : CartesianExponentiable g] :
    CartesianExponentiable (f â‰« g) where
  functor := (Î _ f) â‹™ (Î _ g)
  adj := ofNatIsoLeft (gexp.adj.comp fexp.adj) (baseChange.comp f g).symm

/-- The conjugate isomorphism between pushforward functors. -/
def pushforwardCompIso [HasPullbacks C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    [fexp : CartesianExponentiable f] [gexp : CartesianExponentiable g] :
    fexp.functor â‹™ gexp.functor â‰… (comp f g).functor :=
  conjugateIsoEquiv (gexp.adj.comp fexp.adj) ((comp f g).adj) (baseChange.comp f g)

-- Noncomputably choose finite products from knowledge of their mere existence.
attribute [local instance] ChosenFiniteProducts.ofFiniteProducts

/-- An arrow with a pushforward is exponentiable in the slice category. -/
instance exponentiableOverMk [HasFiniteWidePullbacks C] {I : C} (f : X âŸ¶ I)
    [CartesianExponentiable f] : Exponentiable (Over.mk f) where
  rightAdj :=  (Î”_ f) â‹™ (Î _ f)
  adj := by
    apply ofNatIsoLeft _ _
    Â· exact ((Î”_ f) â‹™ (Î£_ f))
    Â· exact Adjunction.comp CartesianExponentiable.adj (Over.mapPullbackAdj _)
    Â· exact baseChange.natIsoTensorLeftOverMk f

end CartesianExponentiable
