/-
Copyright (c) 2025 Wojciech Nawrocki. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Wojciech Nawrocki
-/
import Poly.UvPoly.Basic
import Poly.Bifunctor.Sigma

noncomputable section

namespace CategoryTheory.UvPoly
open Limits Over ExponentiableMorphism Functor

universe v u
variable {C : Type u} [Category.{v} C] [HasPullbacks C] {E B : C} (P : UvPoly E B)

/-- Sends `(Î“, X)` to `Î£(b : Î“ âŸ¶ B), ğ’(pullback b P.p, X)`.
This is equivalent to the type of partial product cones with apex `Î“` over `X`. -/
@[simps! obj_obj map_app]
def partProdsOver : Cáµ’áµ– â¥¤ C â¥¤ Type v :=
  Functor.Sigma
    ((Over.equivalence_Elements B).functor â‹™ (Over.pullback P.p).op â‹™
      (Over.forget E).op â‹™ coyoneda (C := C))

@[simp]
theorem partProdsOver_obj_map {X Y : C} (Î“ : Cáµ’áµ–) (f : X âŸ¶ Y) (x : (P.partProdsOver.obj Î“).obj X) :
    (P.partProdsOver.obj Î“).map f x = âŸ¨x.1, x.2 â‰« fâŸ© := by
  dsimp [partProdsOver]
  have : ğŸ™ Î“.unop â‰« x.1 = x.1 := by simp
  ext : 1
  . simp
  . dsimp
    rw! (castMode := .all) [this]
    simp

variable [HasTerminal C]

/-- `ğ’(Î“, Pâ‚šX) â‰… Î£(b : Î“ âŸ¶ B), ğ’(b*p, X)` -/
def iso_Sigma (P : UvPoly E B) :
    P.functor â‹™â‚‚ coyoneda (C := C) â‰… P.partProdsOver :=
  calc
    P.functor â‹™â‚‚ coyoneda (C := C) â‰…
        (star E â‹™ pushforward P.p) â‹™â‚‚ (Over.forget B â‹™â‚‚ coyoneda (C := C)) :=
      Iso.refl _

    _ â‰… (star E â‹™ pushforward P.p) â‹™â‚‚ Functor.Sigma
        ((equivalence_Elements B).functor â‹™ coyoneda (C := Over B)) :=
      compâ‚‚_isoWhiskerLeft _ (forget_iso_Sigma B)

    _ â‰… Functor.Sigma
        ((equivalence_Elements B).functor â‹™
          star E â‹™â‚‚ pushforward P.p â‹™â‚‚ coyoneda (C := Over B)) :=
      -- Q: better make `compâ‚‚_Sigma` an iso and avoid `eqToIso`?
      eqToIso (by simp [compâ‚‚_Sigma])

    _ â‰… _ :=
      let i :=
        calc
          star E â‹™â‚‚ pushforward P.p â‹™â‚‚ coyoneda (C := Over B) â‰…
              star E â‹™â‚‚ (Over.pullback P.p).op â‹™ coyoneda (C := Over E) :=
            compâ‚‚_isoWhiskerLeft (star E) (Adjunction.coyoneda_iso <| adj P.p).symm

          _ â‰… (Over.pullback P.p).op â‹™ star E â‹™â‚‚ coyoneda (C := Over E) :=
            Iso.refl _

          _ â‰… (Over.pullback P.p).op â‹™ (Over.forget E).op â‹™ coyoneda (C := C) :=
            isoWhiskerLeft (Over.pullback P.p).op (Adjunction.coyoneda_iso <| forgetAdjStar E).symm;

      Functor.Sigma.isoCongrRight (isoWhiskerLeft _ i)

def equiv (Î“ X : C) : (Î“ âŸ¶ P.functor.obj X) â‰ƒ (b : Î“ âŸ¶ B) Ã— (pullback b P.p âŸ¶ X) :=
  Iso.toEquiv <| (P.iso_Sigma.app (.op Î“)).app X

theorem equiv_app (Î“ X : C) (be : Î“ âŸ¶ P.functor.obj X) :
    P.equiv Î“ X be = (P.iso_Sigma.hom.app <| .op Î“).app X be := by
  dsimp [equiv]

lemma equiv_naturality_left {Î” Î“ : C} (Ïƒ : Î” âŸ¶ Î“) (X : C) (be : Î“ âŸ¶ P.functor.obj X) :
    P.equiv Î” X (Ïƒ â‰« be) =
      let p := P.equiv Î“ X be
      âŸ¨Ïƒ â‰« p.1, pullback.lift (pullback.fst .. â‰« Ïƒ) (pullback.snd ..)
        (Category.assoc (obj := C) .. â–¸ pullback.condition) â‰« p.2âŸ© := by
  conv_lhs => rw [equiv_app, coyoneda.compâ‚‚_naturalityâ‚‚_left, â† equiv_app]
  simp

-- NOTE(WN): As of Lean 4.25, kernel typechecking time is down to 1s from 10s?!
lemma equiv_naturality_right {Î“ X Y : C} (be : Î“ âŸ¶ P.functor.obj X) (f : X âŸ¶ Y) :
    equiv P Î“ Y (be â‰« P.functor.map f) =
      let p := equiv P Î“ X be
      âŸ¨p.1, p.2 â‰« fâŸ© := by
  conv_lhs => rw [equiv_app, coyoneda.compâ‚‚_naturalityâ‚‚_right, â† equiv_app]
  simp

end CategoryTheory.UvPoly
