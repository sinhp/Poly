/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour, Emily Riehl
-/
import Poly.ForMathlib.CategoryTheory.LocallyCartesianClosed.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

/-!
# Beck-Chevalley natural transformations and natural isomorphisms

We construct the so-called Beck-Chevalley natural transformations and isomorphisms through the
repeated applications of the mate construction in the vertical and horizontal directions.

## Main declarations

- `Over.mapIsoSquare`: The isomorphism between the functors `Over.map h â‹™ Over.map g` and
  `Over.map f â‹™ Over.map k` for a commutative square of morphisms `h â‰« g = f â‰« k`.

- `Over.pullbackMapTwoSquare`: The Beck-Chevalley natural transformation of a commutative
  square of morphisms `h â‰« g = f â‰« k`.

- `Over.pullbackForgetTriangle`: The natural transformation `pullback f â‹™ forget X âŸ¶ forget Y`.

- `Over.pullbackIsoSquare`: The isomorphism between the pullbacks along a commutative square of
  morphisms `h â‰« g = f â‰« k`.

- `Over.pushforwardBeckChevalleySquare`: The Beck-Chevalley natural transformation for a commutative
  square of morphisms `h â‰« g = f â‰« k` in the slice category `Over Y`.

- `Over.pushforwardSquareIso`: The isomorphism between the pushforwards along a commutative
  square of morphisms `h â‰« g = f â‰« k`.

## Implementation notes
The lax naturality squares are constructed by the mate equivalence `mateEquiv` and
the natural iso-squares are constructed by the more special conjugation equivalence
`conjugateIsoEquiv`.

## References

The methodology and the notation of the successive mate constructions to obtain the Beck-Chevalley
natural transformations and isomorphisms are based on the following paper:

* [A 2-categorical proof of Frobenius for fibrations defined from a generic point,
in Mathematical Structures in Computer Science, 2024][Hazratpour_Riehl_2024]

-/

noncomputable section
namespace CategoryTheory

open Category Functor Adjunction Limits NatTrans ExponentiableMorphism

universe v u

namespace Over

variable {C : Type u} [Category.{v} C]

section BeckChevalleyTrans

--h â‰« g = f â‰« k -- h â†’ k
theorem map_square_eq {X Y Z W : C} {h : X âŸ¶ Z} {f : X âŸ¶ Y} {g : Z âŸ¶ W} {k : Y âŸ¶ W}
    (sq : CommSq h f g k := by aesop) :
    Over.map h â‹™ Over.map g = Over.map f â‹™ Over.map k := by
  rw [â† mapComp_eq, sq.w, mapComp_eq]

/-- Promoting the equality `mapSquare_eq` of functors to an isomorphism.
```
        Over X -- .map h -> Over Z
           |                  |
   .map f  |         â‰…        | .map g
           â†“                  â†“
        Over Y -- .map k -> Over W
```
The Beck Chevalley transformations are iterated mates of this isomorphism in the
horizontal and vertical directions. -/
def mapIsoSquare {X Y Z W : C} {h : X âŸ¶ Z} {f : X âŸ¶ Y}  {g : Z âŸ¶ W} {k : Y âŸ¶ W}
    (sq : CommSq h f g k := by aesop) :
    Over.map h â‹™ Over.map g â‰… Over.map f â‹™ Over.map k :=
  eqToIso (map_square_eq sq)

variable [HasPullbacks C]

variable {X Y Z W : C} (h : X âŸ¶ Z) (f : X âŸ¶ Y) (g : Z âŸ¶ W) (k : Y âŸ¶ W)
  (sq : CommSq h f g k)

/-- The Beck-Chevalley natural transformation `pullback f â‹™ map h âŸ¶ map k â‹™ pullback g`
constructed as a mate of `mapIsoSquare`:
```
          Over X -- .map h -> Over Z
             â†‘                  â†‘
.pullback f  |         â†˜        | .pullback g
             |                  |
          Over Y -- .map k -> Over W
```
-/
--pullbackBeckChevalleySquare
def pullbackMapTwoSquare : TwoSquare (pullback f) (map k) (map h) (pullback g) :=
  mateEquiv (mapPullbackAdj f) (mapPullbackAdj g) (mapIsoSquare sq).hom

/--
The natural transformation `pullback f â‹™ forget X âŸ¶ forget Y â‹™ ðŸ­ C`
as the mate of the isomorphism `mapForget f`:
```
Over Y -- .pullback f -> Over X
  |                        |
  | .forget Y  â†˜         | .forget X
  V                        V
  C --------- ðŸ­ ---------- C
```
-/
def pullbackForgetTwoSquare : TwoSquare (pullback f) (forget Y) (forget X) (ðŸ­ C) :=
  mateEquiv (mapPullbackAdj f) Adjunction.id (mapForget f).inv

theorem isCartesian_pullbackForgetTwoSquare {X Y : C} (f : X âŸ¶ Y) :
    NatTrans.IsCartesian (pullbackForgetTwoSquare f) := by
  unfold pullbackForgetTwoSquare
  simp only [mateEquiv_apply]
  repeat apply IsCartesian.comp; apply isCartesian_of_isIso
  apply IsCartesian.comp
  . apply IsCartesian.whiskerRight
    apply isCartesian_mapPullbackAdj_counit
  . apply isCartesian_of_isIso

/-- The natural transformation `pullback f â‹™ forget X âŸ¶ forget Y`, a variant of
`pullbackForgetTwoSquare`. -/
--pullbackForgetBeckChevalleyTriangle
def pullbackForgetTriangle :
    pullback f â‹™ forget X âŸ¶ forget Y :=
  pullbackForgetTwoSquare f

/-- The natural transformation `pullback f â‹™ map h âŸ¶ map h'` for a triangle `f : h âŸ¶ h'`. -/
--pullbackMapBeckChevalleyTriangle
def pullbackMapTriangle (h' : Y âŸ¶ Z) (w : f â‰« h' = h) :
    pullback f â‹™ map h âŸ¶ map h' := by
  let iso := (mapComp f h').hom
  rw [w] at iso
  rw [â† Functor.comp_id (map h)] at iso
  exact (mateEquiv (mapPullbackAdj f) Adjunction.id) iso

/-- The isomorphism between the pullbacks along a commutative square.  This is constructed as the
conjugate of the `mapIsoSquare`.
```
          Over X â†--.pullback h-- Over Z
             â†‘                       â†‘
.pullback f  |          â‰…            | .pullback g
             |                       |
          Over Y â†--.pullback k-- Over W
```
-/
--pullbackSquareIso
def pullbackIsoSquare : pullback k â‹™ pullback f â‰… pullback g â‹™ pullback h :=
  conjugateIsoEquiv ((mapPullbackAdj f).comp (mapPullbackAdj k))
  ((mapPullbackAdj h).comp (mapPullbackAdj g)) (mapIsoSquare sq)

/-- The Beck-Chevalley natural transformation
`pushforward g â‹™ pullback k âŸ¶ pullback h â‹™ pushforward f` constructed as a mate of
`pullbackMapTwoSquare`.
```
              Over X â†-.pullback h-- Over Z
                |                     |
.pushforward f  |          â†–          | .pushforward g
                â†“                     â†“
              Over Y â†-.pullback k-- Over W
```
-/
--pushforwardBeckChevalleySquare
def pushforwardPullbackTwoSquare
    [ExponentiableMorphism f] [ExponentiableMorphism g] :
    TwoSquare (pushforward g) (pullback h) (pullback k) (pushforward f) :=
  conjugateEquiv (mapPullbackAdj k |>.comp <| adj g) (adj f |>.comp <| mapPullbackAdj h)
    (pullbackMapTwoSquare h f g k sq)

/--
A variant of `pushforwardTwoSquare` involving `star` instead of `pullback`.
-/
--pushforwardStarBeckChevalleySquare
def starPushforwardTriangle [HasBinaryProducts C] [ExponentiableMorphism f]  :
    star Y âŸ¶ star X â‹™ pushforward f := by
  let iso := (starPullbackIsoStar f).hom
  rw [â† Functor.id_comp (star X)] at iso
  exact (mateEquiv Adjunction.id (adj f)) iso

/-- The conjugate isomorphism between the pushforwards along a commutative square.
```
            Over X --.pushforward h -â†’ Over Z
               |                        |
.pushforward f |           â‰…            | .pushforward g
               â†“                        â†“
            Over Y --.pushforward k -â†’ Over W
```
-/
def pushforwardIsoSquare [ExponentiableMorphism f] [ExponentiableMorphism g]
    [ExponentiableMorphism h] [ExponentiableMorphism k] :
    pushforward h â‹™ pushforward g â‰… pushforward f â‹™ pushforward k :=
  conjugateIsoEquiv (adj g |>.comp <| adj h) (adj k |>.comp <| adj f) (pullbackIsoSquare h f g k sq)

end BeckChevalleyTrans

end Over

section BeckChevalleyComponents

variable {C : Type u} [Category.{v} C]

namespace IsPullback

/--
In a commutative cube diagram if the front, back and the right face are pullback squares then
the the left face is also a pullback square.
```
          P ---pâ‚‚------  X
         /|             /|
    iâ‚„  / |       iâ‚‚   / |
       /  |           /  | fâ‚‚
      Q ----qâ‚‚-----  Z   |
      |   |          |   |
      |   W -fâ‚----- | - S
  qâ‚  |  /           |  /
      | / iâ‚         | / iâ‚ƒ
      |/             |/
      Y ----gâ‚------ T
```
-/
theorem left_face_of_comm_cube {P W X Y Q Z S T : C}
    (pâ‚ : P âŸ¶ W) (pâ‚‚ : P âŸ¶ X) (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S)
    (qâ‚ : Q âŸ¶ Y) (qâ‚‚ : Q âŸ¶ Z) (gâ‚ : Y âŸ¶ T) (gâ‚‚ : Z âŸ¶ T)
    (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T) (iâ‚„ : P âŸ¶ Q)
    (sq_top : CommSq pâ‚‚ iâ‚„ iâ‚‚ qâ‚‚)
    (sq_bot : CommSq fâ‚ iâ‚ iâ‚ƒ gâ‚)
    (sq_left : CommSq iâ‚„ pâ‚ qâ‚ iâ‚)
    (pb_back : IsPullback pâ‚‚ pâ‚ fâ‚‚ fâ‚)
    (pb_front : IsPullback qâ‚‚ qâ‚ gâ‚‚ gâ‚)
    (pb_right : IsPullback iâ‚‚ fâ‚‚ gâ‚‚ iâ‚ƒ) :
    IsPullback iâ‚„ pâ‚ qâ‚ iâ‚ := by
  have paste_horiz_pb := paste_horiz pb_back pb_right
  rw [sq_top.w, sq_bot.w] at paste_horiz_pb
  exact of_right paste_horiz_pb sq_left.w pb_front

/--
In a commutative cube diagram if the front, the left and the right face are pullback squares then
the the back face is also a pullback square.
```
          P ---pâ‚‚------  X
         /|             /|
    iâ‚„  / |       iâ‚‚   / |
       /  |           /  | fâ‚‚
      Q ----qâ‚‚-----  Z   |
      |   |          |   |
      |   W -fâ‚----- | - S
  qâ‚  |  /           |  /
      | / iâ‚         | / iâ‚ƒ
      |/             |/
      Y ----gâ‚------ T
```
-/
theorem back_face_of_comm_cube {P W X Y Q Z S T : C}
    (pâ‚ : P âŸ¶ W) (pâ‚‚ : P âŸ¶ X) (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S)
    (qâ‚ : Q âŸ¶ Y) (qâ‚‚ : Q âŸ¶ Z) (gâ‚ : Y âŸ¶ T) (gâ‚‚ : Z âŸ¶ T)
    (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T) (iâ‚„ : P âŸ¶ Q)
    (sq_top : CommSq pâ‚‚ iâ‚„ iâ‚‚ qâ‚‚)
    (sq_bot : CommSq fâ‚ iâ‚ iâ‚ƒ gâ‚)
    (sq_back : CommSq pâ‚‚ pâ‚ fâ‚‚ fâ‚)
    (pb_front : IsPullback qâ‚‚ qâ‚ gâ‚‚ gâ‚)
    (pb_left : IsPullback iâ‚„ pâ‚ qâ‚ iâ‚)
    (pb_right : IsPullback iâ‚‚ fâ‚‚ gâ‚‚ iâ‚ƒ) :
    IsPullback pâ‚‚ pâ‚ fâ‚‚ fâ‚ := by
  have paste_horiz_pb := paste_horiz pb_left pb_front
  rw [â† sq_top.w, â† sq_bot.w] at paste_horiz_pb
  exact of_right paste_horiz_pb sq_back.w pb_right

/-- The pullback comparison map `pullback.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ` between two
pullback squares is an isomorphism if  `iâ‚` is an isomorphism and one of the
connecting morphisms is an isomorphism. -/
theorem pullback.map_isIso_of_pullback_right_of_comm_cube {W X Y Z S T : C}
    (fâ‚ : W âŸ¶ S) (fâ‚‚ : X âŸ¶ S) [HasPullback fâ‚ fâ‚‚]
    (gâ‚ : Y âŸ¶ T) (gâ‚‚ : Z âŸ¶ T) [HasPullback gâ‚ gâ‚‚]
    (iâ‚ : W âŸ¶ Y) (iâ‚‚ : X âŸ¶ Z) (iâ‚ƒ : S âŸ¶ T)
    (sq_bot : CommSq fâ‚ iâ‚ iâ‚ƒ gâ‚)
    [IsIso iâ‚] (pb_right : IsPullback iâ‚‚ fâ‚‚ gâ‚‚ iâ‚ƒ) :
    IsIso (pullback.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ sq_bot.w pb_right.w.symm) := by
  let m := pullback.map fâ‚ fâ‚‚ gâ‚ gâ‚‚ iâ‚ iâ‚‚ iâ‚ƒ sq_bot.w pb_right.w.symm
  have sq_top : CommSq (pullback.snd fâ‚ fâ‚‚) m iâ‚‚ (pullback.snd gâ‚ gâ‚‚) := by
    aesop
  have sq_left : CommSq m (pullback.fst fâ‚ fâ‚‚) (pullback.fst gâ‚ gâ‚‚) iâ‚ := by
    aesop
  let pb' : IsPullback m (pullback.fst fâ‚ fâ‚‚)  (pullback.fst gâ‚ gâ‚‚) iâ‚ := by
    apply IsPullback.left_face_of_comm_cube (sq_top := sq_top) (sq_bot := sq_bot)
      (sq_left := sq_left) (pb_back := (IsPullback.of_hasPullback fâ‚ fâ‚‚).flip)
      (pb_front := (IsPullback.of_hasPullback gâ‚ gâ‚‚).flip)
      (pb_right := pb_right)
  have is_iso : IsIso m := IsPullback.isIso_fst_of_isIso pb'
  exact is_iso

end IsPullback

variable [HasPullbacks C]

variable {X Y Z W : C} {h : X âŸ¶ Z} {f : X âŸ¶ Y}  {g : Z âŸ¶ W} {k : Y âŸ¶ W}
(sq : CommSq h f g k) (A : Over Y)

open IsPullback Over

theorem mapPullbackAdj.counit_app_left  :
    ((mapPullbackAdj f).counit.app A).left = pullback.fst _ _ := by
  simp only [mapPullbackAdj_counit_app, homMk_left]

@[simp]
theorem pullbackMapTwoSquare_app :
    (pullbackMapTwoSquare h f g k sq).app A =
    Over.homMk (pullback.map _ _ (A.hom â‰« k) _ _ h k (id_comp _).symm sq.w.symm) (by aesop) := by
  ext
  simp only [homMk_left, pullbackMapTwoSquare, mapIsoSquare]
  aesop

theorem forget_map_pullbackMapTwoSquare :
    (forget Z).map ((pullbackMapTwoSquare h f g k sq).app A) =
    pullback.map _ _ _ _ (ðŸ™ _) h k (id_comp _).symm sq.w.symm := by
  simp only [forget_map, pullbackMapTwoSquare_app, homMk_left]

theorem isIso_forgetMappullbackMapTwoSquare_of_isPullback (pb : IsPullback h f g k) :
    IsIso ((forget Z).map ((pullbackMapTwoSquare h f g k pb.toCommSq).app A)) := by
  rw [forget_map_pullbackMapTwoSquare (sq:= pb.toCommSq)]
  let paste_horiz_pb := paste_horiz (IsPullback.of_hasPullback f A.hom) pb
  apply pullback.map_isIso_of_pullback_right_of_comm_cube
  assumption'
  aesop

/-- The pullback Beck-Chevalley natural transformation of a pullback square is an isomorphism. -/
instance pullbackMapTwoSquare_of_isPullback_isIso (pb : IsPullback h f g k) :
    IsIso (pullbackMapTwoSquare h f g k pb.toCommSq) := by
  apply (config := { allowSynthFailures:= true}) NatIso.isIso_of_isIso_app
  intro A
  have := isIso_forgetMappullbackMapTwoSquare_of_isPullback A pb
  exact ReflectsIsomorphisms.reflects (forget Z)
    ((pullbackMapTwoSquare h f g k pb.toCommSq).app A)

/-- The pullback-map exchange isomorphism. -/
def pullbackMapIsoSquare (pb : IsPullback h f g k) :
    pullback f â‹™ map h â‰… Over.map k â‹™ Over.pullback g := by
  refine @asIso _ _ _ _ (pullbackMapTwoSquare h f g k pb.toCommSq) ?_
  exact pullbackMapTwoSquare_of_isPullback_isIso pb

/-- The functor Beck-Chevalley natural transformation of a pullback square is an isomorphism. -/
instance pushforwardPullbackTwoSquare_of_isPullback_isIso (pb : IsPullback h f g k)
    [ExponentiableMorphism f] [ExponentiableMorphism g] :
    IsIso (pushforwardPullbackTwoSquare h f g k pb.toCommSq) := by
  have := pullbackMapTwoSquare_of_isPullback_isIso pb
  apply conjugateEquiv_iso

/-- The pullback-pushforward exchange isomorphism. -/
def pushforwardPullbackIsoSquare (pb : IsPullback h f g k)
    [ExponentiableMorphism f] [ExponentiableMorphism g] :
    pushforward g â‹™ pullback k â‰… pullback h â‹™ pushforward f := by
  refine @asIso _ _ _ _ (pushforwardPullbackTwoSquare h f g k pb.toCommSq) ?_
  exact pushforwardPullbackTwoSquare_of_isPullback_isIso pb

end BeckChevalleyComponents

end CategoryTheory

end
