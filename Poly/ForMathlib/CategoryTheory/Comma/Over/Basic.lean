/-
Copyright (c) 2025 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sina Hazratpour, Wojciech Nawrocki
-/
import Mathlib.CategoryTheory.Comma.Over.Basic
import Mathlib.CategoryTheory.Comma.StructuredArrow.Basic
import Mathlib.CategoryTheory.Category.Cat

namespace CategoryTheory

-- morphism levels before object levels. See note [CategoryTheory universes].
universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {T : Type uâ‚} [Category.{vâ‚} T]

namespace Over

@[simp]
theorem mk_eta {X : T} (U : Over X) : mk U.hom = U := rfl

/-- A variant of `homMk_comp` that can trigger in `simp`. -/
@[simp]
lemma homMk_comp' {X Y Z W : T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : Z âŸ¶ W) (fgh_comp) :
    homMk (U := mk (f â‰« g â‰« h)) (f â‰« g) fgh_comp =
    homMk f â‰« homMk (U := mk (g â‰« h)) (V := mk h) g :=
  rfl

@[simp]
lemma homMk_comp'_assoc {X Y Z W : T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : Z âŸ¶ W) (fgh_comp) :
    homMk (U := mk ((f â‰« g) â‰« h)) (f â‰« g) fgh_comp =
    homMk f â‰« homMk (U := mk (g â‰« h)) (V := mk h) g := rfl

@[simp]
lemma homMk_id {X B : T} (f : X âŸ¶ B) (h : ğŸ™ X â‰« f = f) : homMk (ğŸ™ X) h = ğŸ™ (mk f) :=
  rfl

/-- `Over.Sigma Y U` is a shorthand for `(Over.map Y.hom).obj U`.
This is a category-theoretic analogue of `Sigma` for types. -/
abbrev Sigma {X : T} (Y : Over X) (U : Over (Y.left)) : Over X :=
  (map Y.hom).obj U

namespace Sigma

variable {X : T}

lemma hom {Y : Over X} (Z : Over (Y.left)) : (Sigma Y Z).hom = Z.hom â‰« Y.hom := map_obj_hom

/-- `Î£_ ` is functorial in the second argument. -/
def map {Y : Over X} {Z Z' : Over (Y.left)} (g : Z âŸ¶ Z') : (Sigma Y Z) âŸ¶ (Sigma Y Z') :=
  (Over.map Y.hom).map g

@[simp]
lemma map_left {Y : Over X} {Z Z' : Over (Y.left)} {g : Z âŸ¶ Z'} :
    ((Over.map Y.hom).map g).left = g.left := Over.map_map_left

lemma map_homMk_left {Y : Over X} {Z Z' : Over (Y.left)} {g : Z âŸ¶ Z'} :
    map g = (Over.homMk g.left : Sigma Y Z âŸ¶ Sigma Y Z') := rfl

/-- The first projection of the sigma object. -/
@[simps!]
def fst {Y : Over X} (Z : Over (Y.left)) : (Sigma Y Z) âŸ¶ Y := Over.homMk Z.hom

@[simp]
lemma map_comp_fst {Y : Over X} {Z Z' : Over (Y.left)} (g : Z âŸ¶ Z') :
    (Over.map Y.hom).map g â‰« fst Z' = fst Z := by
  ext
  simp [Sigma.fst, Over.w]

/-- Promoting a morphism `g : Î£_Y Z âŸ¶ Î£_Y Z'` in `Over X` with `g â‰« fst Z' = fst Z`
to a morphism `Z âŸ¶ Z'` in `Over (Y.left)`. -/
def overHomMk {Y : Over X} {Z Z' : Over (Y.left)} (g : Sigma Y Z âŸ¶ Sigma Y Z')
    (w : g â‰« fst Z' = fst Z := by aesop_cat) : Z âŸ¶ Z' :=
  Over.homMk g.left (congr_arg CommaMorphism.left w)

end Sigma

end Over

end CategoryTheory
