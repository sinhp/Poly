/-
Copyright (c) 2024 Emily Riehl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Emily Riehl, Sina Hazratpour
-/

-- import Mathlib.CategoryTheory.Adjunction.Mates

import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.Whiskering

import Mathlib.CategoryTheory.Limits.Over
import Mathlib.CategoryTheory.Adjunction.Over
import Mathlib.CategoryTheory.Limits.Shapes.CommSq

import Mathlib.CategoryTheory.Closed.Monoidal
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.CategoryTheory.Limits.Constructions.Over.Basic

import Mathlib.CategoryTheory.Monad.Products

import Poly.TempMates -- Contains an open mathlib PR redoing the mates file


/-!
# Some basic equalities and isomorphisms of composition and base change functors


## Notation

We provide the following notations:

Given a morphism `f : J âŸ¶ I` in a category `C`,
* `Î£_ f` is the functor `Over.map f : Over I â¥¤ Over J`.
* `Î”_ f` is the functor `baseChange f : Over J â¥¤ Over I`.

Given an object `I : C`,
* `Î£_ I` is the functor `Over.forget : Over I â¥¤ C`.
* `Î”_ I` is the functor `Over.star : C â¥¤ Over I`.

For `X Y : Over I`,
* `Î¼_ X Y` is the projection morphism `(Î£_ X.hom).obj ((Î”_ X.hom).obj Y) âŸ¶ Y`
defined via the counit of the adjunction `Î£_ âŠ£ Î”_`, namely `(mapAdjunction Y.hom).counit.app`.
* `Ï€_ X Y` is the projection morphism `(Î£_ X.hom).obj ((Î”_ X.hom).obj Y) âŸ¶ X`
defined via the counit  of the adjunction `Î£_ âŠ£ Î”_` and the isomorphism
`swapIso X Y : (Î£_ X.hom).obj ((Î”_ X.hom).obj Y) â‰… (Î£_ Y.hom).obj ((Î”_ Y.hom).obj X)`.

We prove that `Î¼_ X Y` and `Ï€_ X Y` form a pullback square:

```
  P ---- Î¼ --> â€¢
  |            |
  Ï€            Y
  |            |
  v            v
  â€¢ ---X--->  I
```

### Implementation Notes

(SH): The definitions `Over.pullback` and `mapPullbackAdj` already existed in mathlib.
Later, `Over.baseChange` and `Over.mapAdjunction` were added
which are duplicates, but the latter have additional `simp` lemmas, namely `unit_app` and `counit_app` which makes proving things with
`simp` easier.

We might change instances of `Over.mapAdjunction` to `Over.mapPullbackAdj`.

(SH) : WIP -- adiding `simp` attributes to `Over.forgetAdjStar`. This means
we no longer will need the lemmas in the namespace `Over.forgetAdjStar`.


#### Other diagrams

```

                      .fst
        pullback p f ------> X
          |                  |
          |                  | p
    .snd  |                  |
          v                  v
          J   ---------->    I
                    f
```

Using the notation above, we have
* `hom_eq_pullback_snd` proves that `(Î”_ f Over.mk p).hom` is `pullback.snd`
* `natIsoTensorLeft` proves that `Î”_ f` â‹™ `Î£_ f` is isomorphic to the product functor `f Ã— _` in the slice category `Over I`.

-/

namespace CategoryTheory
-- universe vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ v u

open Category Functor Adjunction Limits NatTrans Over

variable {C : Type*} [Category C]

@[inherit_doc]
prefix:90 "Î£_ " => Over.map

@[inherit_doc]
prefix:90 "Î£_ " => Over.forget

@[inherit_doc]
prefix:90 "Î”_ " => Over.baseChange -- we might change this to `Over.pullback` later.

@[inherit_doc]
prefix:90 "Î”_ " => Over.star


namespace Over.forgetAdjStar

variable [HasBinaryProducts C] {I : C}

@[simp]
theorem unit_app_left_eq (X : Over I):
    ((Over.forgetAdjStar I).unit.app X).left = prod.lift X.hom (ğŸ™ X.left) := by
  simp [Over.forgetAdjStar, Adjunction.comp, Equivalence.symm]

@[simp]
theorem unit_app_eq (X : Over I):
    (Over.forgetAdjStar I).unit.app X = homMk (by simp; exact prod.lift X.hom (ğŸ™ _)) := by
  ext
  simp

@[simp]
def counit_app_eq (X : C) :
    ((Over.forgetAdjStar I).counit.app X) = prod.snd := by
  simp [Over.forgetAdjStar, Adjunction.comp, Equivalence.symm]

@[simp]
theorem homEquiv (X : Over I) (A : C) (f : X.left âŸ¶ A) :
    (Over.forgetAdjStar I).homEquiv X A f =
    homMk (V := (Î”_ I).obj A) (prod.lift X.hom f) := by
  rw [homEquiv_unit, unit_app_eq]
  ext
  simp

@[simp]
theorem homEquiv_symm [HasBinaryProducts C] (X : Over I) (A : C) (f : X âŸ¶ (Î”_ I).obj A) :
     ((Over.forgetAdjStar I).homEquiv X A).symm f = f.left â‰« prod.snd := by
   rw [homEquiv_counit, counit_app_eq]
   simp

end Over.forgetAdjStar


section NaturalityOfWhiskering

variable {A B : Type*}
variable [Category A] [Category B]
variable {F G : A â¥¤ B} {H K : B â¥¤ C}

-- Naturality of Î² implies naturality of whiskering; this is not used.
@[simp]
theorem WhiskeringNaturality
    (Î± : F âŸ¶ G) (Î² : H âŸ¶ K) :
    (whiskerRight Î± H) â‰« (whiskerLeft G Î²) = (whiskerLeft F Î²) â‰« (whiskerRight Î± K) := by ext; unfold whiskerLeft; simp

end NaturalityOfWhiskering

section

variable {C : Type u} [Category.{v} C]

@[simp]
lemma pullback.map_id {W X S : C} (f : W âŸ¶ S) (g : X âŸ¶ S) [HasPullback f g] (h) (h') :
    pullback.map f g f g (ğŸ™ W) (ğŸ™ X) (ğŸ™ S) h h' = ğŸ™ (pullback f g) := by
  unfold pullback.map
  ext <;> simp

end

noncomputable section

namespace Over

@[simp]
theorem eqToHom_left {X : C} {U V : Over X} (e : U = V) :
    (eqToHom e).left = eqToHom (e â–¸ rfl : U.left = V.left) := by
  subst e; rfl

theorem mapForget_eq {X Y : C} (f : X âŸ¶ Y) :
    Î£_ f â‹™ Î£_ Y = Î£_ X := by
  fapply Functor.ext
  Â· dsimp [Over, Over.map]; intro x; exact rfl
  Â· intros x y u; simp

/--Equality of functors should be avoided if possible, instead we use the isomorphism version.
For use elsewhere.-/
def mapForgetIso {X Y : C} (f : X âŸ¶ Y) :
    Î£_ f â‹™ Î£_ Y â‰… Î£_ X := eqToIso (mapForget_eq f)

theorem mapComp_eq {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Î£_ f â‹™ Î£_ g = Î£_ (f â‰« g) := by
  fapply Functor.ext
  Â· simp [Over.map, Comma.mapRight]
  Â· intro U V k
    ext
    simp

/- Note (SH) : note tha `mapComp` already exists in mathlib, and indeed the components of
of it are `Iso.refl`.
 -/
def mapCompIso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Î£_ f â‹™ Î£_ g â‰… Î£_ (f â‰« g) := eqToIso (mapComp_eq f g)

end Over

namespace baseChange

open Over MonoidalCategory

/-- For an arrow `f : J âŸ¶ I` and an object `X : Over I`, the base-change of `X` along `f` is `pullback.snd`. -/
lemma obj_hom_eq_pullback_snd [HasPullbacks C] {I J : C} (f : J âŸ¶ I) (X : Over I):
    ((Î”_ f).obj X).hom = pullback.snd := by
  rfl

lemma Over.star_obj_eq_mk_prod_fst [HasBinaryProducts C] (I X : C) :
    (Î”_ I).obj X = Over.mk (prod.fst : I â¨¯ X âŸ¶ I) := by
  simp [Over.star, Over.mk]

variable [HasPullbacks C]

/-- The base-change along `terminal.from` ER: Changed statement from an equality to an isomorphism. Proof of commutativity is stuck because of the rewrite. Perhaps I can do this another way? -/
def terminal_from [HasTerminal C] [HasBinaryProducts C] (I : C) (X : Over (âŠ¤_ C)) :
    (Î”_ (terminal.from I)).obj X â‰… (Î”_ I).obj (X.left) := by
  fapply Over.isoMk
  Â· simp
    have := prodIsoPullback I X.left
    have lem := terminal.hom_ext X.hom (terminal.from X.left)
    rw [â† lem] at this
    exact pullbackSymmetry X.hom (terminal.from I) â‰ªâ‰« this.symm
  Â· simp
    sorry

@[simps!]
def swapIso {I : C} (X Y : Over I) :
    (Î£_ X.hom).obj ((Î”_ X.hom).obj Y) â‰… (Î£_ Y.hom).obj ((Î”_ Y.hom).obj X)  := by
  fapply Over.isoMk
  Â· apply pullbackSymmetry
  Â· simp [pullback.condition]

@[simp]
lemma swap_eq_hom {I : C} {X Y : Over I} :
    ((Î£_ X.hom).obj ((Î”_ X.hom).obj Y)).hom =
    (pullbackSymmetry _ _).hom â‰« ((Î£_ Y.hom).obj ((Î”_ Y.hom).obj X)).hom  := by
  simp [â† pullback.condition]

/-- For `X Y : Over I`, the map `Î¼ := projFst`, defined via the counit of the adjunction
`Î£_ âŠ£ Î”_`, is a morphism form the base-change of `Y` along `X` to `Y` .
```
  P ---- Î¼ --> â€¢
  |            |
  Ï€            Y
  |            |
  v            v
  â€¢ ---X--->  I
```
-/
@[simp]
def projFst {I : C} (X Y : Over I) :
    (Î£_ X.hom).obj ((Î”_ X.hom).obj Y) âŸ¶ Y :=
  (mapAdjunction X.hom).counit.app Y

local notation "Î¼_ "  => projFst

/-- For `X Y : Over I`, the map `Ï€ := projSnd` is a morphism form the base-change of `X` along `Y` to `X`.
```
  P ---- Î¼ --> â€¢
  |            |
  Ï€            Y
  |            |
  v            v
  â€¢ ---X--->  I
```
-/
@[simp]
def projSnd {I : C} (X Y : Over I) :
    (Î£_ X.hom).obj ((Î”_ X.hom).obj Y) âŸ¶ X :=
  (swapIso X Y).hom â‰« (mapAdjunction Y.hom).counit.app X

local notation "Ï€_ "  => projSnd

lemma projFst_eq_pullback_fst {I : C} {X Y : Over I} :
    Î¼_ X Y =
    Over.homMk (by simp; exact pullback.fst) (by simp [pullback.condition]) := by
  simp

lemma projFst_left_eq_pullback_fst {I : C} {X Y : Over I} :
    (Î¼_ X Y).left = pullback.fst := by
  simp

lemma projSnd_eq_pullback_snd {I : C} {X Y : Over I} :
    Ï€_ X Y =
    Over.homMk (by simp; exact pullback.snd) (by simp)  := by
  aesop

lemma projSnd_left_eq_pullback_snd {I : C} {X Y : Over I} :
    (Ï€_ X Y).left = (pullback.snd) := by
  simp

-- Note (SH): We already know that `(Ï€_ X Y)` and `(Î¼_ X Y)` are components of a pullback
-- square in the underlying category `C`. We also know that the binary products in the
-- over category are pullbacks in the base category. Note that the folder
-- `CategoryTheory.Limits.Shapes.Constructions.Over` shows that `Over X`
-- has `J`-indexed products if `C` has `J`-indexed wide pullbacks.
-- We should take advantage of these facts
-- to prove `IsLimit <| BinaryFan.mk (Ï€_ X Y) (Î¼_ X Y) ` in `Over I` rather than giving a
-- direct proof.

def isBinaryProduct {I : C} (X Y : Over I) :
    IsLimit <| BinaryFan.mk (Ï€_ X Y) (Î¼_ X Y) := by
  rw [projFst_eq_pullback_fst, projSnd_eq_pullback_snd]
  fapply IsLimit.mk
  Â· intro s
    fapply Over.homMk
    apply pullback.lift (s.Ï€.app âŸ¨.rightâŸ©).left (s.Ï€.app âŸ¨ .left âŸ©).left (by aesop_cat)
    simp
  Â· rintro s âŸ¨âŸ¨lâŸ©|âŸ¨râŸ©âŸ© <;> apply Over.OverMorphism.ext <;> simp
  Â· intro s m h
    apply Over.OverMorphism.ext
    apply pullback.hom_ext <;> simp
    Â· apply congr_arg CommaMorphism.left (h âŸ¨ .rightâŸ©)
    Â· apply congr_arg CommaMorphism.left (h âŸ¨ .left âŸ©)

/-- The object `(Î£_ X.hom) ((Î”_ X.hom) Y)` is isomorphic to the binary product `X Ã— Y`
in `Over I`. -/
@[simps!]
def isoProd {I : C} (X Y : Over I) [HasBinaryProduct X Y] :
    (Î£_ X.hom).obj ((Î”_ X.hom).obj Y) â‰… Limits.prod X Y := by
  apply IsLimit.conePointUniqueUpToIso (isBinaryProduct X Y) (prodIsProd X Y)

def isoProdmk {I J : C} (f : J âŸ¶ I) (Y : Over I) [HasBinaryProduct (Over.mk f) Y]:
    (Î£_ f).obj ((Î”_ f).obj Y) â‰… Limits.prod (Over.mk f) Y :=
  isoProd (Over.mk f) _

@[simp]
lemma isoProd_comp_fst {I : C} (X Y : Over I) [HasBinaryProduct X Y]:
    (isoProd X Y).hom â‰« prod.fst = (Ï€_ X Y) :=
  IsLimit.conePointUniqueUpToIso_hom_comp (isBinaryProduct X Y) (Limits.prodIsProd X Y) âŸ¨.leftâŸ©

@[simp]
lemma isoProdmk_comp_fst {I J : C} (f : J âŸ¶ I) (Y : Over I) [HasBinaryProduct (Over.mk f) Y] :
    (isoProdmk f Y).hom â‰« prod.fst = (Ï€_ (Over.mk f) Y) :=
  isoProd_comp_fst (Over.mk f) Y

@[simp]
lemma isoProd_comp_snd {I : C} {X Y : Over I} [HasBinaryProduct X Y] :
    (isoProd X Y).hom â‰« prod.snd = (Î¼_ X Y) :=
  IsLimit.conePointUniqueUpToIso_hom_comp (isBinaryProduct X Y) (Limits.prodIsProd X Y) âŸ¨.rightâŸ©

variable [HasFiniteWidePullbacks C] {I : C}

attribute [local instance] monoidalOfHasFiniteProducts

-- NatOverProdIso
/-- The functor composition `(baseChange X.hom) â‹™ (Over.map X.hom)` is naturally isomorphic
to the left tensor product functor `X Ã— _` -/
def natIsoTensorLeft {I : C} (X : Over I) :
    (Î”_ X.hom) â‹™ (Î£_ X.hom) â‰… tensorLeft X := by
  fapply NatIso.ofComponents
  Â· intro Y
    apply isoProd
  Â· intro Y Z f
    simp
    ext1 <;> simp_rw [assoc]
    Â· simp_rw [prod.map_fst, comp_id]
      iterate rw [isoProd_comp_fst]
      ext
      simp
    Â· simp_rw [prod.map_snd]
      iterate rw [isoProd_comp_snd, â† assoc, isoProd_comp_snd]
      ext
      simp

def natIsoTensorLeftOverMk {I J : C} (f : J âŸ¶ I) :
    (Î”_ f) â‹™ (Î£_ f) â‰… tensorLeft (Over.mk f) := by
  apply natIsoTensorLeft (Over.mk f)

/--
The isomorphism between the base change functors obtained as the conjugate of the `mapForgetIso`.
For use elsewhere.-/
def mapStarIso [HasBinaryProducts C] [HasPullbacks C] {X Y : C} (f : X âŸ¶ Y) :
    Î”_ X â‰… Î”_ Y â‹™ Î”_ f :=
  conjugateIsoEquiv (Over.forgetAdjStar X) ((mapAdjunction f).comp (Over.forgetAdjStar Y)) (mapForgetIso f)

def id (I : C) : Î”_ (ğŸ™ I) â‰… ğŸ­ _ :=
  conjugateIsoEquiv (mapAdjunction (ğŸ™ I)) Adjunction.id (mapId I).symm

/- Note (SH): This has already been done in `Over.pullbackComp`. What is different in this variant? -/
/-- The conjugate isomorphism between pullback functors. -/
def comp [HasPullbacks C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    Î”_ (f â‰« g) â‰… Î”_ g â‹™ Î”_ f :=
  conjugateIsoEquiv (mapAdjunction (f â‰« g)) ((mapAdjunction f).comp (mapAdjunction g)) (mapCompIso f g)

end Over

end baseChange
namespace Limits

/- Note (SH) : In general, in `Poly` project, we use `IsPullback` instead of `HasPullback`. -/
@[simp]
lemma pullback.map_id {W X S : C} (f : W âŸ¶ S) (g : X âŸ¶ S) [HasPullback f g] (h) (h') :
    pullback.map f g f g (ğŸ™ W) (ğŸ™ X) (ğŸ™ S) h h' = ğŸ™ (Limits.pullback f g) := by
  simp only [pullback.map]
  ext <;> simp

end Limits

variable {C : Type*} [Category C] [HasPullbacks C]

-- Proof by Markus Himmel (with commentary by Dagur Asgeirsson)
@[simps]
def toOverTerminal' (T : C) (h : IsTerminal T) : C â¥¤ Over T where
  obj X := Over.mk (h.from _)
  map f := Over.homMk f

/-- Note (SH): the difference between this and `Î”_ (âŠ¤_ C)` is that we only use the
assumption that `C` has only terminal but not binary products.
On the other hand, I recommend using `Î”_ (âŠ¤_ C)` in general since we know more facts
about it such its adjunction (top of the file).
-/
def toOverTerminal [HasTerminal C] : C â¥¤ Over (âŠ¤_ C) :=
  toOverTerminal' (âŠ¤_ C) terminalIsTerminal

def equivOverTerminal' (T : C) (h : IsTerminal T) : C â‰Œ Over T :=
  CategoryTheory.Equivalence.mk (toOverTerminal' T h) (Over.forget _)
    (NatIso.ofComponents (fun X => Iso.refl _))
    (NatIso.ofComponents (fun X => Over.isoMk (Iso.refl _) (by simpa using h.hom_ext _ _)))

def equivOverTerminal [HasTerminal C] : C â‰Œ Over (âŠ¤_ C) :=
  equivOverTerminal' (âŠ¤_ C) terminalIsTerminal

def isoOverTerminal [HasTerminal C] : Cat.of (ULift C) â‰… Cat.of (Over (âŠ¤_ C)) where
  hom := {
    obj  := fun âŸ¨XâŸ© => by
      exact Over.mk (terminalIsTerminal.from X)
    map := @fun âŸ¨XâŸ© âŸ¨YâŸ© f => by
      exact Over.homMk f
  }
  inv := {
    obj := fun X => sorry
    map := sorry
  }
  hom_inv_id := sorry
  inv_hom_id := sorry

def toOverTerminalStarIso [HasTerminal C] [HasBinaryProducts C] : Î”_ (âŠ¤_ C) â‰… toOverTerminal := by
  have := Iso.refl (Î£_ (âŠ¤_ C))
  sorry -- I can't seem to infer that the inverse equivalence used above is an equivalence.
  -- have : (Over.forget (âŠ¤_ C)).IsEquivalence := by infer_instance
  -- have := (Over.forget (âŠ¤_ C)).asEquivalence.toAdjunction
  -- have := conjugateIsoEquiv (Over.forgetAdjStar (âŠ¤_ C)) _ this

def toOverTerminalStarTriangleIso [HasTerminal C] [HasBinaryProducts C] (X : C) :
    Î”_ X â‰… toOverTerminal â‹™ Î”_ (terminal.from X) :=
  baseChange.mapStarIso (terminal.from X) â‰ªâ‰« isoWhiskerRight (toOverTerminalStarIso (C := C)) (Î”_ (terminal.from X))
